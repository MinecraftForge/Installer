import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.github.ben-manes.versions' version '0.49.0'
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'net.minecraftforge.gradleutils' version '[2.3,2.4)'
}

group = 'net.minecraftforge'
version = gradleutils.tagOffsetVersion
println "Version: $version"

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
}

test {
  useJUnitPlatform()
}

dependencies {
    implementation(libs.jopt.simple)
    implementation(libs.gson)
    testImplementation(libs.junit.api)
    testRuntimeOnly(libs.bundles.junit.runtime)
}


def installerFiles = new FileNameFinder().getFileNames(projectDir.absolutePath, '*-installer.jar')
if (!installerFiles.isEmpty()) {
    def first = file(installerFiles.get(0))
    logger.lifecycle("Detected test installer: " + first.name)
    dependencies.implementation(files(first))
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes('Main-Class': 'net.minecraftforge.installer.SimpleInstaller')
        attributes([
            'Specification-Title':    'Installer',
            'Specification-Vendor':   'Forge Development LLC',
            'Specification-Version':   gradleutils.gitInfo.tag,
            'Implementation-Title':   'SimpleInstaller',
            'Implementation-Vendor':  'Forge Development LLC',
            'Implementation-Version':  project.version
        ] as LinkedHashMap, 'net/minecraftforge/installer/')
    }
}

tasks.named('shadowJar', ShadowJar).configure {
    archiveClassifier = 'fatjar'
    minimize()
}

compileJava {
    options.encoding = 'UTF-8'
}

artifacts {
    archives shadowJar
}

changelog {
    fromBase()
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        artifactId = 'installer'

        pom {
            name = 'Installer'
            description = 'Minecraft Forge Installer'
            url = 'https://github.com/MinecraftForge/Installer'

            gradleutils.pom.setGitHubDetails(pom, 'Installer')

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}
