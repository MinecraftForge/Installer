buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:6.0.3'
    }
}

plugins {
  id 'net.minecrell.licenser' version '0.3'
  id 'org.ajoberstar.grgit' version '2.3.0'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

group = 'net.minecraftforge'
archivesBaseName = 'installer'
version = gitVersion()
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

def gitVersion() {
    def raw = grgit.describe(longDescr: true)
    def desc = (raw == null ? 'unknown-offset-hash' : grgit.describe(longDescr: true)).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name
    project.ext.SPEC_VERSION = tag
    return "${tag}.${offset}" //${t -> if (branch != 'master') t << '-' + branch}"
}

license {
  header project.file('LICENSE-header.txt')
  include 'net/minecraftforge/installer/**/*.java'
  newLine false
}

ext {
    MAIN_CLASS = 'net.minecraftforge.installer.SimpleInstaller'
    MANIFEST = manifest{
        attributes('Main-Class': MAIN_CLASS)
        attributes([
            'Specification-Title':      'Installer',
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    SPEC_VERSION,
            'Implementation-Title':     'SimpleInstaller',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'Forge Development LLC'
        ] as LinkedHashMap, 'net/minecraftforge/installer/')
    }
}

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'
    compile 'com.google.code.gson:gson:2.8.5'
}

compileJava {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest.from(MANIFEST)
}

shadowJar {
    classifier 'fatjar'
    manifest.from(MANIFEST)
}

task pgShrinkJar(type: proguard.gradle.ProGuardTask, dependsOn: shadowJar) {
    ext {
        inputJar = shadowJar.archivePath
        outDir = file("${buildDir}/proguard")
        obfuscatedJar = "${outDir}/${jar.baseName}.jar"
        config = 'proguard.pro'
    }
    inputs.file inputJar
    inputs.file config
    
    outDir.mkdirs()

    injars inputJar
    outjars obfuscatedJar
    libraryjars configurations.compile

    // Automatically handle the Java version of this build.
    if (System.getProperty('java.version').startsWith('1.')) {
        // Before Java 9, the runtime classes were packaged in a single jar file.
        libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    } else {
        // As of Java 9, the runtime classes are packaged in modular jmod files.
        libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
        libraryjars "${System.getProperty('java.home')}/jmods/java.desktop.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
        libraryjars "${System.getProperty('java.home')}/jmods/java.logging.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
        libraryjars "${System.getProperty('java.home')}/jmods/jdk.unsupported.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
    }
    configuration config
}

task shrinkJar(type: Jar, dependsOn: pgShrinkJar) {
    classifier = 'shrunk'
    from zipTree(pgShrinkJar.obfuscatedJar)
    manifest.from(MANIFEST)
}

artifacts {
    //archives shadowJar
    archives sourcesJar
    archives shrinkJar
}

jar {
    manifest.from(MANIFEST)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact shrinkJar
            artifact jar
            pom {
                name = project.archivesBaseName
                packaging = 'jar'
                description = 'Minecraft Forge Installer'
                url = 'https://github.com/MinecraftForge/Installer'

                scm {
                    url = 'https://github.com/MinecraftForge/Installer'
                    connection = 'scm:git:git://github.com/MinecraftForge/Installer.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/Installer.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/Installer/issues'
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty('forgeMavenPassword')) {
                credentials {
                    username project.properties.forgeMavenUser
                    password project.properties.forgeMavenPassword
                }
                url 'https://files.minecraftforge.net/maven/manage/upload'
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}
