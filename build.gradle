buildscript {
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.1.0'
    }
}

plugins {
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.minecraftforge.gradleutils' version '2.+'
    //id 'maven'
    id 'maven-publish'
    id 'java'
    id 'eclipse'
    id 'idea'
}

repositories {
    mavenCentral()
}

group = 'net.minecraftforge'
archivesBaseName = 'installer'
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

license {
  header project.file('LICENSE-header.txt')
  include 'net/minecraftforge/installer/**/*.java'
  newLine false
}

version = gradleutils.getTagOffsetBranchVersion(null, 'master', 'HEAD', '2.0')
println('Installer Version: ' + version)

final String SPEC_VERSION = gradleutils.gitInfo.tag
final String MAIN_CLASS = 'net.minecraftforge.installer.SimpleInstaller'
final MANIFEST = manifest {
    attributes('Main-Class': MAIN_CLASS)
    attributes([
            'Specification-Title':      'Installer',
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    SPEC_VERSION,
            'Implementation-Title':     'SimpleInstaller',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'Forge Development LLC'
    ], 'net/minecraftforge/installer/')
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'com.google.code.gson:gson:2.9.0'
	testImplementation(platform('org.junit:junit-bom:5.7.2'))
	testImplementation('org.junit.jupiter:junit-jupiter')
}

tasks.named('test', Test).configure {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}

tasks.named('compileJava', JavaCompile).configure {
    options.encoding = 'UTF-8'
}

tasks.register('sourcesJar', Jar) {
    dependsOn 'classes'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    manifest.from(MANIFEST)
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
    archiveClassifier = 'fatjar'
    manifest.from(MANIFEST)
}

tasks.register('pgShrinkJar', proguard.gradle.ProGuardTask) {
    dependsOn 'shadowJar'

    final File inputJar = shadowJar.archiveFile.get().asFile
    final File outDir = file("${buildDir}/proguard")
    final String config = 'proguard.pro'

    inputs.file inputJar
    inputs.file config
    
    outDir.mkdirs()

    injars inputJar
    outjars "${outDir}/installer.jar"

    // Automatically handle the Java version of this build.
    if (System.getProperty('java.version').startsWith('1.')) {
        // Before Java 9, the runtime classes were packaged in a single jar file.
        libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    } else {
        // As of Java 9, the runtime classes are packaged in modular jmod files.
        libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
        libraryjars "${System.getProperty('java.home')}/jmods/java.desktop.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
        libraryjars "${System.getProperty('java.home')}/jmods/java.logging.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
        libraryjars "${System.getProperty('java.home')}/jmods/jdk.unsupported.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
    }
    configuration config
}

tasks.register('shrinkJar', Jar) {
    dependsOn 'pgShrinkJar'
    archiveClassifier = 'shrunk'
    from zipTree(file("${buildDir}/proguard/installer.jar"))
    manifest.from(MANIFEST)
}

artifacts {
    //archives shadowJar
    archives sourcesJar
    archives shrinkJar
}

tasks.named('jar', Jar).configure {
    manifest.from(MANIFEST)
}

tasks.register('testJar', Jar) {
    from zipTree(shrinkJar.archiveFile.get().asFile)
    from sourceSets.test.output
    archiveClassifier = 'test'
    manifest.from(MANIFEST)
}

publishing {
    publications.register('mavenJava', MavenPublication) {
            artifact sourcesJar
            artifact shrinkJar
            artifact jar
            pom {
                name = project.archivesBaseName
                packaging = 'jar'
                description = 'Minecraft Forge Installer'
                url = 'https://github.com/MinecraftForge/Installer'

                scm {
                    url = 'https://github.com/MinecraftForge/Installer'
                    connection = 'scm:git:git://github.com/MinecraftForge/Installer.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/Installer.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/Installer/issues'
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (System.getenv().MAVEN_USER) {
                url 'https://maven.minecraftforge.net/'
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = System.getenv().MAVEN_USER ?: 'not'
                    password = System.getenv().MAVEN_PASSWORD ?: 'set'
                }
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}
